% $Author$
% $Date$
% $Revision$

% HISTORY:
% 2007-03-30 - Cassou splits of Streams from Collection chapter
% 2007-07-05 - Cassou partial draft complete?
% 2007-08-02 - Stef pass
% 2007-08-16 - Cassou continues
% 2007-08-21 - Oscar edit
% 2007-08-21 - Cassou review
% 2009-07-07 - Oscar migrate to Pharo; fixed broken tests

%=================================================================
\ifx\wholebook\relax\else
% --------------------------------------------
% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../common.tex}
	\pagestyle{headings}
	\setboolean{lulu}{true}
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../common.tex}
%	\usepackage{a4wide}
% --------------------------------------------
    \graphicspath{{figures/} {../figures/}}
	\begin{document}
	\renewcommand{\nnbb}[2]{} % Disable editorial comments
	\sloppy
\fi
%=================================================================
\chapter{ストリーム}\chalabel{streams}

\ew{Streams are presented as a way to navigate collection. From my point of view, stream are important not to navigate collection, but to produce/consume data:
(a)	memory constraint. Data can not hold into memory and must be processed in a stream fashion, e.g: encryption
(b)	blocking IO. A stream is a nice abstraction to deal with, and the stream manages internally data availability, buffering, etc. to simplify the consumption/production of data
Only few streams have random access capability.}

\clsindexmain{Stream}
ストリームは、要素の並びを繰り返し処理するために用いられます。ここで並びとは、順序付きコレクション、ファイル、ネットワークストリームなどです。
ストリームは読み込み可能か、書き込み可能か、もしくはその両方です。
読み込みまたは書き込みは、常にストリーム内の現在の位置に対して相対的に行われます。
ストリームは簡単にコレクションに変換可能ですし、その逆も同様です。
\lr{"Streams can easily be converted into collections." I wouldn't say it like this, because it is not true for all streams (infinite streams). According to Kent Beck we should only talk about conversion when the same protocol is supported. Collections and Streams do not support the same protocol. (p. 249)}

%=============================================================
\section{二つの「要素の並び」}
ストリームを理解するには、以下のような例えが有効です:
ストリームは二つの「要素の並び」
\ie、過去の要素の並びと未来の要素の並びとして表現することができます。
ストリームはこの二つの並びの間に位置します。
このモデルを理解しておくことは大切です。\st のすべてのストリーム操作はこのモデルによるからです。
このような理由で、ほとんどの \clsind{Stream} クラスは \clsind{PositionableStream} のサブクラスになっています。
\figref{_abcde} は、5 個の文字を含むストリームを表しています。
このストリームは元の位置にあります。\ie、過去の要素は一つもありません。
\mthind{PositionableStream}{reset} メッセージを使えば、いつでもこの位置に戻ることができます。

\begin{figure}[ht]
\centerline{\includegraphics[scale=0.5]{_abcdeStef}}
\caption{元の位置にあるストリーム。}
\figlabel{_abcde}
\vspace{.2in}
\end{figure}

要素の読み込みは、概念的に、未来の要素の並びの先頭を取り除き、それを過去の要素の並びの最後に付け加えることです。
\ct{next} メッセージを使って一つの要素を読むと、ストリームの状態は\figref{a_bcde} のようになります。

\begin{figure}[ht]
\centerline{\includegraphics[scale=0.5]{a_bcdeStef}}
\caption{\figref{_abcde} のストリームに \ct{next} を送信した直後の状態: 文字 \ct{a} は「過去」に属するが \ct{b}、\ct{c}、\ct{d}、\ct{e} は「未来」に属する。}
\figlabel{a_bcde}
\vspace{.2in}
\end{figure}

要素の書き込みは、未来の要素の並びの先頭を新しいものに置き換えて、置き換えられた要素を過去に移動させることです。
\figref{ax_cde} は、\mthind{Stream}{nextPut:} \ct{anElement} メッセージを使って\figref{a_bcde} のストリームに \ct{x} を書き込んだ直後の状態を示しています。

\begin{figure}[ht]
\centerline{\includegraphics[scale=0.5]{ax_cdeStef}}
\caption{\figref{a_bcde} のストリームに \ct{x} を書き込んだ直後の状態。}
\figlabel{ax_cde}
\vspace{.2in}
\end{figure}

%=============================================================
\section{ストリーム対コレクション}

コレクションのプロトコルは、要素の格納と除去と列挙をサポートしますが、
これらの操作を混ぜることはできません。
例えば \clsind{OrderedCollection} の要素を \ct{do:} メソッドによって処理する場合、
\ct{do:} ブロック内で要素の追加や削除はできません。
あるいはコレクションのプロトコルには、二つのコレクションについて同時に繰り返し、
1  回ごとに片方を選んで前へ進めながらもう片方は止めておくようなものはありません。
このような処理を行うには、走査用の添字か位置の参照を、コレクション自身の外部で管理する必要があります:
これはまさに \clsind{ReadStream}、\clsind{WriteStream} そして \clsind{ReadWriteStream} の役割です。

これら三つのクラスは、コレクションを\emph{ストリーム処理}するために定義されています。
例えば、次のコードは \ct{Interval} オブジェクトに対するストリームを作り、
ストリームから二つの要素を読み込みます。
\needlines{5}
\begin{code}{@TEST |r|}
r := ReadStream on: (1 to: 1000).
r next.   --> 1
r next.   --> 2
r atEnd.--> false
\end{code}

\ct{WriteStream} を使えばコレクションにデータを書き込むことができます:
\begin{code}{@TEST |w|}
w := WriteStream on: (String new: 5).
w nextPut: $a.
w nextPut: $b.
w contents. -->  'ab'
\end{code}

読み込みと書き込みの両方のプロトコルをサポートする \ct{ReadWriteStream} を使うこともできます。

\pharo の \ct{WriteStream} と \ct{ReadWriteStream} の主な問題は、配列と文字列しかサポートしていないことです。
現在開発中の Nile という新しいライブラリがこの状況を変えつつありますが、
今のところそれ以外のコレクションをストリーム処理すると、エラーになります\footnote{訳注: このコードはエラーにはなりません。}: %@ 識者に確認

\needlines{3}
\begin{code}{}
w := WriteStream on: (OrderedCollection new: 20).
w nextPut: 12. -->  エラー
\end{code}

ストリームはコレクション専用ではありません。ファイルやソケットに対しても使えます。
次の例は \ct{test.txt} というファイルを作り、二つの文字列を改行で区切って書き込み、ファイルを閉じます。

\needlines{3}
\begin{code}{}
StandardFileStream
  fileNamed: 'test.txt'
  do: [:str | str
                nextPutAll: '123';
                cr;
                nextPutAll: 'abcd'].
\end{code}
\cmindex{FileStream class}{fileNamed:do:}

次の節では、ストリームプロトコルについてさらに詳しく解説します。

%=============================================================
\section{コレクションに対するストリーム処理}

ストリームはコレクションを扱うのにとても便利で、
コレクションの要素の読み込みと書き込みに使うことができます。
コレクションに対するストリーム機能について探っていきましょう。

%-----------------------------------------------------------------
\subsection{コレクションの読み込み}

この節ではコレクションの読み込みに使われる機能について見ていきます。
コレクションの読み込みにストリームを使うことは、本質的にはコレクションへのポインタを得ることを意味します。
ポインタは読み込みによって前へ進み、また希望する場所へ自由に移動させることができます。
\clsindmain{ReadStream} クラスを使ってコレクションの要素を読み込みます。

\mthind{ReadStream}{next} メソッドでコレクションから要素を 1 個読み取り、\mthind{ReadStream}{next:} メソッドで任意個の要素を読み取ります。

\begin{code}{@TEST |stream|}
stream := ReadStream on: #(1 (a b c) false).
stream next. -->   1
stream next. -->   #(#a #b #c)
stream next. -->   false
\end{code}
\cmindex{PositionableStream class}{on:}

\begin{code}{@TEST |stream|}
stream := ReadStream on: 'abcdef'.
stream next: 0. -->   ''
stream next: 1. -->   'a'
stream next: 3. -->   'bcd'
stream next: 2. -->   'ef'
\end{code}

\mthind{PositionableStream}{peek} メッセージは、前へ進まずにストリームの次の要素を調べたいときに使います。

\begin{code}{@TEST |stream negative number|}
stream := ReadStream on: '-143'.
negative := (stream peek = $-).    "前へ進まずに最初の要素を調べる。"
negative. --> true
negative ifTrue: [stream next].       "マイナス文字を無視する"
number := stream upToEnd.
number. --> '143'
\end{code}
\noindent
このコードは、ストリーム中の数の符合の有無を真偽値変数 \ct{negative} に設定し、数の絶対値を \ct{number} に設定します。
\mthind{ReadStream}{upToEnd} メソッドは、現在の位置から最後までのストリームのすべてを返し、ストリームを最後の位置に設定します。
このコードは、\mthind{PositionableStream}{peekFor:} メソッドを使うことで単純化できます。\ct{peekFor:} は、ストリームの次の要素がパラメータと同じときは前へ進み、異なるときは進みません。

\begin{code}{@TEST |stream negative number|}
stream := '-143' readStream.
(stream peekFor: $-) --> true
stream upToEnd         --> '143'
\end{code}
\noindent
\ct{peekFor:} はまた、パラメータと次の要素が等しかったかかどうかを示す真偽値を返します。

上の例を見て、ストリームを作る新しい方法に気付いたかもしれません:
順序付きコレクションに対して単に \mthind{SequenceableCollection}{readStream} を送信すれば、そのコレクションに対する読み込みストリームを作ることができます。

\paragraph{位置決め。} ストリームのポインタの位置を設定するメソッドがあります。
もし添字がわかっていれば、\mthind{PositionableStream}{position:} で直接その位置へ移動できます。
\mthind{PositionableStream}{position} で現在の位置を求めることができます。
ストリームは要素そのものを指しているわけではなく、要素と要素の間を指していることを覚えておいて下さい。
ストリームの先頭に対応する添え字は 0 です。

\figref{ab_cde} に描かれたストリームの状態を次のコードで得ることができます:

\begin{code}{@TEST |stream|}
stream := 'abcde' readStream.
stream position: 2.
stream peek --> $c
\end{code}

\begin{figure}[h!t]
\centerline{\includegraphics[scale=0.5]{ab_cdeStef}}
\caption{位置 2 にあるストリーム}
\figlabel{ab_cde}
\vspace{.2in}
\end{figure}

ストリームを先頭か末尾に移動させるには、
% If you want to go to the beginning or at the end is what you want, 
それぞれ \mthind{PositionableStream}{reset}、\mthind{PositionableStream}{setToEnd} を使うことができます。
\mthind{PositionableStream}{skip:} と \mthind{PositionableStream}{skipTo:} は、現在の位置を基準として前へ進む場合に使います。
\ct{skip:} は引数に数を取ることができ、その数だけ要素をスキップします。
一方 \ct{skipTo:} は、パラメータと同じ要素が見つかるまでストリームの要素をすべてスキップします。
このときストリームは、見つかった要素の後ろに位置決めされることに注意して下さい。

\begin{code}{@TEST |stream|}
stream := 'abcdef' readStream.
stream next.        --> $a    "ストリームは今、a の真後ろに位置している"
stream skip: 3.                           "ストリームは今、d の後ろ"
stream position.  -->   4
stream skip: -2.                          "ストリームは b の後ろ"
stream position.  --> 2
stream reset.
stream position.  --> 0
stream skipTo: $e.                      "ストリームは今、e の真後ろ"
stream next.        --> $f
stream contents. --> 'abcdef'
\end{code}

ご覧のように文字 \ct{e} はスキップされています。

\mthind{PositionableStream}{contents} メソッドは、常にストリーム全体のコピーを返します。

\paragraph{テスト。}現在のストリームの状態をテストするためのメソッドがあります。
\mthind{PositionableStream}{atEnd} は、これ以上読める要素がない場合のみ真を返します。
\mthind{PositionableStream}{isEmpty} は、コレクションにまったく要素がない場合のみ真を返します。

\ct{atEnd} を使ったアルゴリズムの実装例を示します。これは二つのソート済みのコレクションをパラメータとして取り、それらをマージして新たなソート済みのコレクションにします:

\needlines{4}
\begin{code}{@TEST |stream1 stream2 result|}
stream1 := #(1 4 9 11 12 13) readStream.
stream2 := #(1 2 3 4 5 10 13 14 15) readStream.

"変数 result にはソート済みのコレクションが入る。"
result := OrderedCollection new.
[stream1 atEnd not & stream2 atEnd not]
  whileTrue: [stream1 peek < stream2 peek
    "両方のストリームの要素のうち小さい方を取り除き、result に加える。"
    ifTrue: [result add: stream1 next]
    ifFalse: [result add: stream2 next]].

"どちらかのストリームは終端に来ていないかもしれない。残りすべてをコピーする。"
result
  addAll: stream1 upToEnd;
  addAll: stream2 upToEnd.

result. -->   an OrderedCollection(1 1 2 3 4 4 5 9 10 11 12 13 13 14 15)
\end{code}

%-----------------------------------------------------------------
\subsection{コレクションへの書き込み}

\ct{ReadStream} を使ってコレクションの要素を繰り返し読み取る方法は、既に見てきました。
次に、\clsindmain{WriteStream} を使ってコレクションを作る方法を学びましょう。

\ct{WriteStream} は、コレクションの様々な場所にたくさんのデータを加えるとき便利です。
次の例のように、静的な部分と動的な部分からなる文字列を生成するときにしばしば用いられます:

\begin{code}{NB: can't be tested due to the changing number of classes}
stream := String new writeStream.
stream
  nextPutAll: 'This Smalltalk image contains: ';
  print: Smalltalk allClasses size;
  nextPutAll: ' classes.';
  cr;
  nextPutAll: 'This is really a lot.'.

stream contents. --> 'This Smalltalk image contains: 2322 classes.
This is really a lot.'
\end{code}

このテクニック\footnote{訳注: 書き込みストリームにメッセージのカスケードを送信するテクニック。}は例えば、様々なクラスの \ct{printOn:} メソッドの実装でも使われています。

単に文字列を生成するためだけにストリームを用いる場合、より単純で効率的な方法があります: %@ もともとなかった段落を補ったり意訳したりした。

\begin{code}{@TEST |string|}
string := String streamContents:
		[:stream |
			stream
                 print: #(1 2 3);
                 space;
                 nextPutAll: 'size';
                 space;
                 nextPut: $=;
                 space;
                 print: 3.	].
string. -->   '#(1 2 3) size = 3'
\end{code}

\mthind{SequenceableCollection class}{streamContents:}\seclabel{streamContents} メソッドは、コレクションとそのコレクションへの書き込みストリームを作ります。
このメソッドは与えられたブロックを実行しますが、このときこのストリームをパラメータとしてブロックに渡します。
ブロックが終了すると \ct{streamContents:} は、コレクションの中身を返します。

このようなコンテキスト\footnote{訳注: 書き込みストリームにアクセス可能なコンテキスト。\ct{'This Smalltalk image contains: ...'} のコード例もその一つ。}では、以下の \ct{WriteStream} のメソッドが特に便利です:

\begin{description}
\item[\lct{nextPut:}] パラメータをストリームに追加する;
\item[\lct{nextPutAll:}] パラメータとして渡されたコレクションの要素をそれぞれストリームに追加する;
\item[\lct{print:}] パラメータのテキスト表現をストリームに追加する。
	\cmindex{Stream}{print:}
\end{description}

特殊な文字をストリームに表示するのに便利な \mthind{WriteStream}{space}、\mthind{WriteStream}{tab}、\mthind{WriteStream}{cr} (carriage return) などのメソッドもあります。%@ different kinds of → 「特殊な」とした
もうひとつ便利なメソッドは、\mthind{WriteStream}{ensureASpace} です。これはストリームの最後の文字がスペースをあることを保証します; ストリームの最後の文字がスペースでない場合にはスペースを追加します。

\paragraph{文字(列)の連結について。}
\ct{WriteStream} に \mthind{WriteStream}{nextPut:}、\mthind{WriteStream}{nextPutAll:} を送信することは多くの場合、文字(列)を連結する最良の方法になります。
カンマ演算子 (\ct{,}) による結合は、これらに比べるとはるかに非効率的です:
\index{Collection!カンマ演算子}

\begin{code}{}
[| temp |
  temp := String new.
  (1 to: 100000)
    do: [:i | temp := temp, i asString, ' ']] timeToRun --> 115176 "(milliseconds)"

[| temp |
  temp := WriteStream on: String new.
  (1 to: 100000)
    do: [:i | temp nextPutAll: i asString; space].
  temp contents] timeToRun --> 1262 "(milliseconds)"
\end{code}

ストリームを使った方がずっと効率的になり得るのは、カンマ演算子はレシーバと引数を連結した新たな文字列を作成するので、レシーバと引数の両方をコピーしなければならないからです。
もし同じレシーバに対してカンマ演算子で連結を繰り返すと、連結するごとに文字列が長くなり、
コピーしなければならない文字数は指数関数的に増えてしまいます。%@ なぜ指数関数?
またこの方法は、メモリ上に回収しなければならないゴミをたくさん残します。
カンマ演算子の代わりにストリームを使うことは、最適化の技法としてよく知られています。
\lr{About Concatenation. This is not true for real world examples (the example benchmark is unrealistic). Most of the time concatenation is simpler, cleaner and much faster, for example when being used on a small number of longer strings. (p. 257)}
実際、\mthind{SequenceableCollection class}{streamContents:} メソッド (\pageref{sec:streamContents} ページで述べました)が、ストリームを用いた文字列の連結作業を助けてくれます:

\begin{code}{}
String streamContents: [ :tempStream |
  (1 to: 100000)
       do: [:i | tempStream nextPutAll: i asString; space]] 
\end{code}

%-----------------------------------------------------------------
\subsection{読み込みと書き込みを同時に行う}

ストリームを介してコレクションへの読み込みアクセス、書き込みアクセスを同時に行うこともできます。
ウェブブラウザの進む・戻るボタンの動作を管理する \ct{History} (履歴)クラスが作りたかったとしましょう。
履歴は \ref{fig:emptyStream} から \ref{fig:page4} のような動作をします。

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{emptyStef}}
\caption{新しい履歴は空。ウェブブラウザには何も表示されていない。}
\figlabel{emptyStream}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page1Stef}}
\caption{ユーザはページ 1 を開く。}
\figlabel{page1}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page2Stef}}
\caption{ユーザはページ 2 へのリンクをクリックする。}
\figlabel{page2}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page3Stef}}
\caption{ユーザはページ 3 へのリンクをクリックする。}
\figlabel{page3}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page2_Stef}}
\caption{ユーザは戻るボタンをクリックする。今、再びページ 2 を見ている。}
\figlabel{page2_}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page1_Stef}}
\caption{ユーザは再び戻るボタンをクリックする。ページ 1 が表示されている。}
\figlabel{page1_}
\vspace{.2in}
\end{figure}

\begin{figure}[!ht]
\centerline{\includegraphics[scale=0.5]{page4Stef}}
\caption{ユーザはページ 1 からページ 4 へのリンクをクリックする。ページ 2、ページ 3 の履歴はなくなる。}
\figlabel{page4}
\vspace{.2in}
\end{figure}

この振る舞いは \clsind{ReadWriteStream} を使って実装できます。

\needlines{6}
\begin{code}{}
Object subclass: #History
  instanceVariableNames: 'stream'
  classVariableNames: ''
  poolDictionaries: ''
  category: 'PBE-Streams'

History>>>initialize
    super initialize.
    stream := ReadWriteStream on: Array new.
\end{code}

ここでは特に難しいことはありません。
ストリームを持つ新しいクラスを定義します。
このストリームは \ct{initialize} メソッドで作られます。

次に、進むと戻るのメソッドが必要です:

\begin{code}{}
History>>>goBackward
  self canGoBackward ifFalse: [self error: 'Already on the first element'].
  stream skip: -2.
  ^ stream next.

History>>>goForward
  self canGoForward ifFalse: [self error: 'Already on the last element'].
  ^ stream next
\end{code}

ここまでのコードは実に単純明快です。
次に、ユーザがリンクをクリックしたときに動作する \ct{goTo:} メソッドに取りかかりましょう。
例えば次のようなコードを思いつくかもしれません:

\begin{code}{}
History>>>goTo: aPage
    stream nextPut: aPage.
\end{code}

しかしこれは完全ではありません。
ユーザがリンクをクリックした後は、それ以上は進めるページがあってはいけないからです。\ie、進むボタンは無効にならなければいけません。
これを実現する最も簡単な方法は、すぐ後ろに \ct{nil} を書き込んで履歴の最後を示すことです:

\begin{code}{}
History>>>goTo: anObject
  stream nextPut: anObject.
  stream nextPut: nil.
  stream back.
\end{code}

もはや実装しなければならないメソッドは、\ct{canGoBackward} と \ct{canGoForward} だけになりました。

ストリームは常に二つの要素の間に位置しています。
戻るためには現在の位置より前に二つのページがなければいけません:
一つは現在のページ、もう一つは戻りたいページです。

\begin{code}{}
History>>>canGoBackward
  ^ stream position > 1

History>>>canGoForward
  ^ stream atEnd not and: [stream peek notNil]
\end{code}

ストリームの中身を確認するメソッドを追加しましょう:
\begin{code}{}
History>>>contents
  ^ stream contents
\end{code}

これで履歴が予告どおりに動きます:
\begin{code}{}
History new
	goTo: #page1;
	goTo: #page2;
	goTo: #page3;
	goBackward;
	goBackward;
	goTo: #page4;
	contents --> #(#page1 #page4 nil nil)
\end{code}

%=============================================================
\section{ストリームをファイルアクセスに使う}

コレクションをストリーム処理する方法については、既に見てきたとおりです。
同じように、ハードディスクのファイルもストリーム処理することができます。
いったん作成されると、ファイルに対するストリームはコレクションに対するストリームとほとんど変わりません:
同じプロトコルでストリームの読み込み、書き込み、位置決めができます。
主な違いはストリームの作成方法にあります。
ファイルストリームの作成について、いくつかの異なる方法を見ていきましょう。

%-----------------------------------------------------------------
\subsection{ファイルストリームを作成する}
\seclabel{creat-file-stre}

ファイルストリームを作るには、以下のインスタンス作成メソッドのいずれかを用いなければなりません。これらのメソッドは、\clsindmain{FileStream} クラスが提供します:

\begin{description}

\item[\lct{fileNamed:}] 与えられた名前のファイルを、読み込みと書き込みのために開きます。
  ファイルが既に存在していた場合、その内容は変更されたり置き換えられたりしますが、ファイルを閉じるときに切り捨てられません。
  ファイル名にディレクトリ部が明示されていない場合、ファイルはデフォルトディレクトリ内に作られます。
  \cmindex{FileStream class}{fileNamed:}
  
\item[\lct{newFileNamed:}] 与えられた名前の新しいファイルを作り、そのファイルに書き込むためのストリームを返します。
  ファイルが既に存在していた場合、ユーザにどうするか尋ねます。
  \cmindex{FileStream class}{newFileNamed:}
  
\item[\lct{forceNewFileNamed:}] 与えられた名前の新しいファイルを作り、そのファイルに書き込むためのストリームを返します。
  ファイルが既に存在していた場合、黙ってそのファイルを削除した後に新しいファイルを作ります。
  \cmindex{FileStream class}{forceNewFileNamed:}

\item[\lct{oldFileNamed:}] 与えられた名前の既存のファイル\footnote{訳注: ファイルが存在しなかった場合、ユーザにどうするか尋ねます。}を、読み込みと書き込みのために開きます。
  ファイルが既に存在していた場合、その内容は変更されたり置き換えられたりしますが、ファイルを閉じるときに切り捨てられません。
  ファイル名にディレクトリ部が明示されていない場合、ファイルはデフォルトディレクトリ内に作られます。
  \cmindex{FileStream class}{oldFileNamed:}

\item[\lct{readOnlyFileNamed:}] 与えられた名前の既存のファイルを、読み込みのために開きます。
  \cmindex{FileStream class}{readOnlyFileNamed:}

\end{description}

開いたファイルストリームは忘れずに閉じなければなりません。
これには \mthind{FileStream}{close} メソッドを使います。

\begin{code}{@TEST |stream|}
stream := FileStream forceNewFileNamed: 'test.txt'.
stream
    nextPutAll: 'This text is written in a file named ';
    print: stream localName.
stream close.

stream := FileStream readOnlyFileNamed: 'test.txt'.
stream contents. --> 'This text is written in a file named ''test.txt'''
stream close.
\end{code}

% \on{need way to clean up test files afterwards}

%[:fileName | (FileDirectory forFileName: fileName)
%	deleteFileNamed: fileName
%	ifAbsent: [ 'Could not delete ', fileName ] ]
%	value: 'test.txt'

ファイルストリームに \mthind{FileStream}{localName} を送信すると、パス名の最後の要素が返ります。
\mthind{StandardFileStream}{fullName} を送信してフルパス名を得ることもできます。

すぐに、ファイルストリームを手作業で閉じるのが苦痛でエラーの元であることに気付くでしょう。
\mthind{FileStream class}{forceNewFileNamed:do:} は、この問題を解消するために用意されています。このメソッドは、書き込みストリームをパラメータとするブロックを評価してすべての書き込みをすませた後、自動的にこのストリームを閉じます。同様に読み込みストリーム用に \mthind{FileStream class}{readOnlyFileNamed:do:} があります。%@ 「同様に」以降は自主的に補った

\begin{code}{@TEST |string|}
FileStream
    forceNewFileNamed: 'test.txt'
    do: [:stream |
        stream
            nextPutAll: 'This text is written in a file named ';
            print: stream localName].
string := FileStream
            readOnlyFileNamed: 'test.txt'
            do: [:stream | stream contents].
string --> 'This text is written in a file named ''test.txt'''
\end{code}

ファイルストリーム作成メソッドのうちブロックを引数に取るものは、まずストリームを作成し、次にストリームを引数としてブロックを実行し、最後にストリームを閉じます。
これらのメソッドは、ブロックが返すものを返します。つまりブロックの最後の式の値を返します。
前の例では、ブロックの最後の式: \ct{stream contents} を評価してファイルの中身を得、その値を変数 \ct{string} に代入しています。

%-----------------------------------------------------------------
\subsection{バイナリストリーム}
\seclabel{binary-streams}

デフォルトでファイルストリームは、テキストベースつまり文字の読み書き用に作られます。
ストリームでバイナリデータを扱う場合は、ストリームに \mthind{FileStream}{binary} メッセージを送信しなければなりません。

ストリームがバイナリモードの場合、0 から 255 (1 バイト)の数値しか書き込めません。
\ct{nextPutAll:} メソッドを使って一度に二つ以上の数値を書きたいときは、\clsind{ByteArray} オブジェクトを引数として渡さなければなりません。

\begin{code}{@TEST}
FileStream
  forceNewFileNamed: 'test.bin'
  do: [:stream |
          stream
            binary;
            nextPutAll: #(145 250 139 98) asByteArray].

FileStream
  readOnlyFileNamed: 'test.bin'
  do: [:stream |
          stream binary.
          stream size.         --> 4
          stream next.         --> 145
          stream upToEnd. --> #[250 139 98]
      ].
\end{code}

次の例では「test.pgm」という画像ファイルを作成します (PGM: portable graymap)。
作成したファイルは、お気に入りのドロープログラムで開くことができます。

% The following does not assert anything, but @TEST is used to ensure
% that no error is thrown.
\begin{code}{@TEST}
FileStream
  forceNewFileNamed: 'test.pgm' 
  do: [:stream |
	stream
		nextPutAll: 'P5'; cr;
		nextPutAll: '4 4'; cr;
		nextPutAll: '255'; cr;
		binary;
		nextPutAll: #(255 0 255 0) asByteArray;
		nextPutAll: #(0 255 0 255) asByteArray;
		nextPutAll: #(255 0 255 0) asByteArray;
		nextPutAll: #(0 255 0 255) asByteArray
	]
\end{code}

これは\figref{checkerboard4x4} のような 4x4 のチェッカーボード(市松模様)を作ります。

\begin{figure}[!ht]
\centerline{\includegraphics[width=0.25\textwidth]{checkerboard4x4}}
\caption{バイナリストリームで描くことができる 4x4 のチェッカーボード(市松模様)}
\figlabel{checkerboard4x4}
\vspace{.2in}
\end{figure}

%=============================================================
\section{まとめ}

ストリームは、要素の並びをインクリメンタルに読み書きする場合に、コレクションよりも良い方法を提供します。
ストリームとコレクションを相互に変換する簡単な方法があります。

\begin{itemize}
  \item ストリームは読み込み可能、書き込み可能、あるいは読み書きともに可能です。
  \item コレクションをストリームに変換する場合、コレクション「に対する (on)」ストリームを定義します(\eg \ct{ReadStream on: (1 to: 1000)})。あるいはコレクションに \ct{readStream} \etc{}のメッセージを送信します。
  \item ストリームをコレクションに変換する場合、\ct{contents} メッセージを送信します。
  \item 大きなコレクションを連結する場合は、カンマ演算子を使うよりもストリームを使った方が効率的です。つまりストリームを作り、\ct{nextPutAll:} でコレクションをストリームに追加し、\ct{contents} を送信して結果を取り出します。
  \item ファイルストリームはデフォルトで文字型です。これを明示的にバイナリにするには、\ct{binary} メッセージを送信します。
\end{itemize}

%=================================================================
\ifx\wholebook\relax\else\end{document}\fi
%=================================================================

%-----------------------------------------------------------------

%%% Local Variables: 
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: t
%%% TeX-PDF-mode: t
%%% ispell-local-dictionary: "english"
%%% End:
